<?xml version="1.0" encoding="UTF-8"?>
<html
  lang="{./inLanguage}"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:xi="http://www.w3.org/2001/XInclude"
	xmlns:py="py"
  >
  <head>
	<meta charset="UTF-8" />

	<!-- Tags for search engines -->
	<title>
	  <xsl:value-of select="./name" /> - <xsl:value-of select="../../sitename" />
	</title>
	<meta name="description" content="{normalize-space(./teaser)}" />
	<link rel="canonical" href="{$self-url}" />
	<meta name="robots" content="index, follow" />
	<meta name="viewport" content="width=device-width, initial-scale=1.0" />

	<!-- Opengraph tags for Facebook and others -->
	<meta property="og:title" content="{./name}" />
	<meta property="og:type" content="website" />
	<meta property="og:url" content="{$self-url}" />
	<meta property="og:image" content="{./image/url}" />
	<meta property="og:description" content="{normalize-space(./teaser)}" />
	<meta property="og:locale" content="{py:replace(./inLanguage, '-', '_')}" />
	<meta property="og:sitename" content="{../../sitename}" />

	<!-- Twitter card -->
	<meta property="twitter:card" content="summary" />
	<meta property="twitter:site" content="{../../twitter}" />
	<meta property="twitter:creator" content="{./author/person/extra/twitter}" />
	<meta property="twitter:description" content="{normalize-space(./teaser)}" />
	<meta property="twitter:title" content="{./name}" />
	<meta property="twitter:image" content="{./image/url}" />
	<meta property="twitter:image:alt" content="{./image/caption}" />

	<!-- TOOD: favicons -->

	<script>
	<xi:include href="main.js" parse="text" />
	</script>
	<style>
	  <xi:include href="main.css" parse="text" />
	</style>
  </head>
  <body vocab="http://schema.org/">
	<nav
	  xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	  <xsl:for-each select="../../nav/*">
		<a href="{@href}" class="{@class}">
		  <xsl:value-of select="." />
		</a>
	  </xsl:for-each>
	</nav>
	<main id="column">
	  <article typeof="BlogPosting">
		<header>
		  <h1 property="name">
			<a href="{$self-url}">
			  <py:fix_phrase lang="{./inLanguage}" allow_words="{./fix_text/@allow_words}">
				<xsl:value-of select="./name" />
			  </py:fix_phrase>
			</a>
		  </h1>
		  <p class="byline">
			by
			<span property="author">
			  <xsl:apply-templates select="./author" />
			</span>
			on
			<py:date>
			  <xsl:value-of select="./datePublished" />
			</py:date>
		  </p>
		  <div property="image">
			<xsl:apply-templates select="./image" />
		  </div>
		  <xsl:if test="string-length(normalize-space(./name)) &gt; 70">
			<py:warn>Title of "<xsl:value-of select="./name"/>" is too long for Twitter.</py:warn>
		  </xsl:if>
		  <xsl:if test="string-length(normalize-space(./teaser)) &gt; 200">
			<py:warn>Teaser of "<xsl:value-of select="./name"/>" is too long for Twitter.</py:warn>
		  </xsl:if>
		  <py:ignore>
			<py:fix_para lang="{./inLanguage}" allow_words="{./fix_text/@allow_words}">
			  <xsl:value-of select="./teaser" />
			</py:fix_para>
		  </py:ignore>
		</header>
		  <xsl:for-each select="./articleBody/*">
			<xsl:choose>
			  <xsl:when test="name(.) = 'p'">
				<py:fix_para lang="{../../inLanguage}" allow_words="{../../fix_text/@allow_words}">
				  <xsl:copy-of select="." />
				</py:fix_para>
			  </xsl:when>
			  <xsl:when test="name(.) = 'h2'">
				<h2 id="{py:slugify(.)}">
				  <a href="#{py:slugify(.)}">
					<py:fix_phrase lang="{../../inLanguage}" allow_words="{../../fix_text/@allow_words}">
					  <xsl:value-of select="." />
					</py:fix_phrase>
				  </a>
				</h2>
			  </xsl:when>
			  <xsl:when test="name(.) = 'image'">
				<xsl:apply-templates select="." />
			  </xsl:when>
			  <xsl:otherwise>
				<py:warn>Unknown element `<xsl:value-of select="name(.)"/>` in "<xsl:value-of select="./name"/>"</py:warn>
			  </xsl:otherwise>
			</xsl:choose>
		  </xsl:for-each>
		<footer>
		  <xi:include href="license.html" parse="xml" />
		  <p class="cite-as">
			Cite as:
			<span property="creditText">
			  “<a href="{$self-url}">
				<xsl:value-of select="./name" />
			  </a>
			  by
			  <xsl:apply-templates select="./author/person" />”
			</span>.
		  </p>
		</footer>
	  </article>
	</main>
  </body>
</html>
